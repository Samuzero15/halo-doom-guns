Actor Halo_Magnum : Weapon	{
	//[Samu] Man! Setting a good resolution was a true hassle!
	//		 Made it a bit faster, and added some effects (from the Zscript version) just for the looks.
	//		 Gun's centered properly, and now contains a reloading function!
	Obituary "%o was took down by %k's Magnum."
	Inventory.PickupMessage		"Magnum Md6 Adquired."
	Inventory.PickupSound		"Halo/Weapons/Mag_MD6/Pickup"
	Weapon.AmmoType1 "Halo_Magnum_Mag"
	Weapon.AmmoGive1 12
	
	Weapon.AmmoType2 "Halo_Magnum_Clip"
	Weapon.AmmoGive2 48
	Tag "Magnum"
	
	Weapon.SlotNumber 2
	Weapon.BobSpeed 1.86
	Weapon.BobRangeX 0.30
	
	+WEAPON.NO_AUTO_SWITCH
	+Weapon.NoAutofire
	
	states{
		Spawn:
			HMAG A -1
			Stop
	
		Ready.Dryfire:
			MAR1 A 12 A_PlaySound("Halo/Weapons/DryFire", CHAN_AUTO)
		Ready:
			TNT1 A 0 A_JumpIfInventory("Halo_WeaponShow", 1, "Select.Anim")
			MAR1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		
		Select:
			TNT1 A 6 A_GiveInventory("Halo_WeaponShow", 1)
			TNT1 A 0 A_Raise
			Wait
			
		Select.Anim:
			TNT1 A 0 A_TakeInventory("Halo_WeaponShow", 1)
			TNT1 A 0 A_PlaySound("Halo/Weapons/Mag_MD6/Select", CHAN_WEAPON)
			MASE ABCDE 1
			goto Ready
		
		Deselect:
			MASE EDCBA 1
			TNT1 A 0 A_Lower
			wait
			
		Fire:
			TNT1 A 0 A_Jumpifinventory("Halo_Magnum_Mag", 1, 1)
			goto Reload
			
			TNT1 A 0 A_PlaySound("Halo/Weapons/Mag_MD6/Fire", CHAN_WEAPON)
			TNT1 A 0 A_Light2
			//TNT1 A 0 A_FireBullets(3.2, 3.2, -1, 145, "BulletPuff", FBF_NORANDOM)
			TNT1 A 0 A_FireCustomMissile("Halo_Magnum_Bullet",frandom(-1.7, 1.7),0,0,4,0, frandom(-1.7, 1.7))
			TNT1 A 0 A_TakeInventory("Halo_Magnum_Mag", 1)
			MAFI AB 1 BRIGHT
			TNT1 A 0 A_Light0
			TNT1 A 0 A_Quake(1,2,0,20,"")
			TNT1 A 0 A_ZoomFactor (0.984)
			MAFI CDE 1
			TNT1 A 0 A_ZoomFactor (1.0)
			MAFI FG 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
			MAR1 AAAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
		Hold:
			MAR1 A 1 
			MAR1 A 1 A_Refire("Hold")
		goto Ready
		
		Reload:
			TNT1 A 0 A_Jumpif(ACS_NamedExecuteWithResult("Halo_RelVal_Magnum"),1)
			goto Ready.Dryfire
			TNT1 A 0 A_PlaySound("Halo/Weapons/Mag_MD6/Reload", CHAN_AUTO)
			MAR1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
			TNT1 A 0 ACS_NamedExecute("Halo_ReloadMagnum", 0)
			MAR2 ABCDEFGHIJKL 1
			MAR1 AAAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
			goto Ready
	}
}

Actor Halo_WeaponShow : Inventory {	Inventory.MaxAmount 1}

Actor Halo_Magnum_Mag : Ammo { Inventory.MaxAmount 12 }

Actor Halo_Magnum_Clip : Ammo replaces Clip 
{
	Inventory.MaxAmount 96 
	Inventory.Amount 12
	Inventory.PickupMessage "Picked up a Magnum Clip"
	Inventory.Icon "CLIPA0"
	States{
		Spawn:
		CLIP A -1
		Stop
	}
}

Const int Halo_Magnum_BulletNerf = 10000;

Actor Halo_Magnum_Bullet : FastProjectile
{
    Radius 2
    Height 2
	Health 1
    decal "Bulletchip"
	renderstyle ADD
    PROJECTILE
      +BLOODSPLATTER
      +DONTSPLASH
	Speed 210
	Damage (ACS_ExecuteWithResult(Halo_Magnum_BulletNerf)) // Starting damage.
   States
   {
   Spawn:
	  PUFF A 1 
	  TNT1 A 0 //ACS_NamedExecuteAlways("Halo_Magnum_BulletDMG",0)
      Loop
   Death: //Walls and Floors
	  TNT1 A 1 
	  TNT1 A 0 
      stop
   }
}